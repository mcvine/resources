/********************************************************************
*
* Instrument: SNS_CNCS
*
* %Identification
* Written by: Andrey Podlesnyak
* Date: 2008-2019
* Origin: SNS 
*
* %INSTRUMENT_SITE: SNS
*
*   The CNCS instrument: Source + chopper system + sample + PSD and tof detector
*
* %Description
*
*  The CNCS TOF spectrometer from chopper system to final detector, with sample.
*
* %Parameters
* Ei: [meV]       Neutron incident energy, meV.  
* f1: [Hz]  Frequency of the Fermi chopper
* f2: [Hz]  Frequency of the first bandwidth chopper
* f3: [Hz]  Frequency of the second bandwidth chopper
* f41: [Hz]  Frequency of the first disk last chopper
* f42: [Hz]  Frequency of the second disk last chopper
* slits41: [deg] First disk slits, 9 deg, 4.4 deg, 2 deg
* slits42: [deg] Second disk slits, 9 deg, 4.4 deg, 2 deg
*
* Chopper phase is calculated as time delay after pulse (time=0)
* according to the equation:                            
* T = T0+Toff + (2286.3*L) / SQRT(Ei)			          
* T0 -  peak flux offset tzero for moderator[mks]    		
* Toff - an offset due who know why [mks]    		  
* L - the distance from the moderator to the chopper  [meters]   
* f - chopper frequency                               [Hz]
*
*       
* %End
*******************************************************************************/
 

DEFINE INSTRUMENT CNCS_test(Ei=3.32,f41=300,f42=300,slits41=9,slits42=9)

DECLARE 	        
%{			
/**** chopper speed, Hz ****/
    double f1,f2,f3;
/*  global guide properties, taken from G. Granroth source */
    double Gu_alpha,Gu_R,Gu_W,Gu_Qc;
/* CNCS distances [m]*/
    double d_mod_mon1,L1,L2,d_mod_mon2,L3,L4,d_mod_mon3,d_mod_sample,d_mod_det;
    double T0,Toff,ffict_tof,ffict_tof_deg,erange,emin,emax;
    double f1_phase,f1_phase_deg,f1_tof,f1_tof_deg;
    double sample_tof,sample_tof_start,sample_tof_stop;
    double det_tof,det_tof_start,det_tof_stop;
    double mon3_tof,mon3_tof_start,mon3_tof_stop;
    double f2_phase,f2_phase_deg,f2_tof,f2_tof_deg;
    double f3_phase,f3_phase_deg,f3_tof,f3_tof_deg;
    double f41_phase,f41omega,f41_tof,f41_tof_deg;
    double f42_phase,f42omega,f42_tof,f42_tof_deg;
%}			

INITIALIZE
%{

/**** chopper speed, Hz ****/
f1=300.0;f2=60.0;f3=60.0;

/*  global guide properties, taken from G. Granroth source */
Gu_alpha=5.0;Gu_R=0.99;Gu_W=0.002;Gu_Qc=0.02;

/* CNCS distances [m]*/
double d_mod_mon1=6.313    ;
double L1=6.413            ; 
double L2=7.515            ;
double d_mod_mon2=7.556    ;     
double L3=33.020           ; 
double L4=34.7845          ; 
double d_mod_mon3=34.836   ;
double d_mod_sample=36.262 ;
double d_mod_det=39.762    ;


/* set an energy range that is +-20% of the desired energy*/
    erange=0.2*Ei;
    emin=Ei-erange;
    emax=Ei+erange;


/* determine peak flux offset tzero [s] for moderator, Ei must be [meV] */
T0=(156.58-66.66*log(Ei)+6.38*log(Ei)*log(Ei)+0.56*log(Ei)*log(Ei)*log(Ei))/1.e6;
Toff=0.0;


/*TOF [s] and phases [deg]               */
/* T = T0(Ei) + (2286.3*L) / SQRT(Ei)    */

     f1_tof=T0+(2286.3*L1)/sqrt(Ei)/1.e6;
     f1_tof_deg=360.0 *f1*f1_tof;

     ffict_tof=T0+(2286.3*6.350)/sqrt(Ei)/1.e6;

     f2_tof=T0+(2286.3*L2)/sqrt(Ei)/1.e6;

     f3_tof=T0+(2286.3*L3)/sqrt(Ei)/1.e6;

     f41_tof=T0+(2286.3*L4)/sqrt(Ei)/1.e6;
     f41omega=f41*2.0*3.1415926;

     f42_tof=T0+(2286.3*L4)/sqrt(Ei)/1.e6;
     f42omega=f42*2.0*3.1415926;

     sample_tof=T0+(2286.3*d_mod_sample)/sqrt(Ei);
     sample_tof_start=sample_tof-150;
     sample_tof_stop=sample_tof+150;

     det_tof=T0+(2286.3*d_mod_det)/sqrt(Ei);
     det_tof_start=sample_tof-150;
     det_tof_stop=sample_tof+150;

     mon3_tof=T0+(2286.3*d_mod_mon3)/sqrt(Ei);
     mon3_tof_start=mon3_tof-150;
     mon3_tof_stop=mon3_tof+150;

%}

TRACE
COMPONENT arm1 = Progress_bar()
AT(0,0,0) ABSOLUTE

/*================================================================== 
A source that produces a time and energy distribution from the SNS moderator files Author: G. Granroth, Date: July 2014,
Origin: SNS Project Oak Ridge National Laboratory
Important: The output units are N/pulse, 
source normalized to 2 MW power, 1.0 GeV proton energy.            
- source_sct21a_td_05_1.dat for old moderator IRP1,
- a1G0AD-5-f5.dat for heavy water cooled IRP2 assuming 100% para hydrogen,
- a1G0AD30-5-f5.dat for heavy water cooled IRP2 assuming ortho/para ratio of 30/70.

*/

COMPONENT sns_source = SNS_source(
/*  filename="source_sct21a_td_05_1.dat", */
    filename="a1G0AD-5-f5.dat", 
    dist=1.0,
    xwidth=0.1,
    yheight=0.12, 
    focus_xw=0.05, 
    focus_yh=0.10, 
    Emin=emin, 
    Emax=emax)
AT (0, 0, 0) RELATIVE PREVIOUS
/*================================================================*/


/*Fiction Tof and Energy monitors for the test purposes only */
/*COMPONENT source_monitor_tof = TOF_monitor(
    nt=140, 
    filename="Source_tof.det", 
    xmin=-0.005, 
    xmax=0.005, 
    ymin=-0.005, 
    ymax=0.005, 
    tmin=0,
    tmax=3000,
    restore_neutron=1)
AT (0, 0, 0.998) RELATIVE arm1*/

/*
COMPONENT source_monitor_e = E_monitor(
    filename="Source_en.det", 
    nE = 100,
    xmin=-0.005, 
    xmax=0.005, 
    ymin=-0.005, 
    ymax=0.005, 
    Emin=0.5, 
    Emax=50, 
    restore_neutron=1)
AT (0, 0, 0.999) RELATIVE arm1 */

/*COMPONENT source_monitor_psd = PSD_monitor(
    nx=100, 
    ny=100, 
    filename="Source_psd.det", 
    xmin=-0.05, 
    xmax=0.05, 
    ymin=-0.1, 
    ymax=0.1, 
    restore_neutron=1)
AT (0, 0, 1.0) RELATIVE arm1*/

/*============================================================*/

/*Neutron guide, 							   
Author: Kristian Nielsen, Origin: Risoe, Date: September 2 1998, 
Version: 1.31  */

/* Guides N1, N2 and N3 */
COMPONENT Guide1 = Guide(
	w1=0.05, 
	h1=0.1, 
	w2=0.05, 
	h2=0.1, 
	l=5.280, 
	m=2.5, 
	R0=Gu_R, 
	Qc=Gu_Qc, 
	W=Gu_W, 
	alpha=Gu_alpha)
AT (0,0,1.002) RELATIVE arm1

/*================================================================*/
/* Fermi Chopper                                                  */
/* nslit=9 for slits 5mm and nslit=22 for the slits 2 mm          */

COMPONENT fermichopper = FermiChopper(
    phase=-f1_tof_deg, 
    radius=0.04,
    nu=f1, 
    nslit=9, 
    length=0.017, 
    eff=0.9, 
    xwidth=0.05)
AT (0, 0, 6.413) RELATIVE arm1

/*============================================================*/

/*Fiction Tof monitor for the test purposes only*/
/*TOF step = 1 muS, must be the same everywhere to compare intensity */
/*COMPONENT fc_monitor_tof = TOF_monitor(
    nt=3001, 
    filename="FC_tof_5mm_60Hz.det", 
    xmin=-0.005, 
    xmax=0.005, 
    ymin=-0.005, 
    ymax=0.005, 
    tmin=7000,
    tmax=10000,
    restore_neutron=1)
AT (0, 0, 6.555) RELATIVE arm1*/

/*============================================================*/

/* Guide N4, in between Fermi chopper and chopper 2*/
COMPONENT Guide4 = Guide(
	w1=0.05, 
	h1=0.1, 
	w2=0.05, 
	h2=0.1, 
	l=0.925, 
	m=2.5, 
	R0=Gu_R, 
	Qc=Gu_Qc, 
	W=Gu_W, 
	alpha=Gu_alpha)
AT (0,0,6.560) RELATIVE arm1

/*============================================================*/

/* Chopper #2  Distance Moderator - CH#2 = 7515 mm, 60Hz */
COMPONENT Chopper2=DiskChopper(
	radius=0.25, 
	theta_0=14.0, 
	yheight=0, 
	nu=f2, 
	nslit=1, 
	delay=f2_tof)
AT (0,0,7.515) RELATIVE arm1

/*============================================================*/

/*Beam monitor #2: Tof monitor */
/*TOF step = 1 muS, must be the same everywhere to compare intensity */
/*COMPONENT Monitor2_tof = TOF_monitor(
    nt=3001, 
    filename="Monitor2_tof.dat", 
    xmin=-0.005, 
    xmax=0.005, 
    ymin=-0.005, 
    ymax=0.005, 
    tmin=8000,
    tmax=11000,
    restore_neutron=1)
AT (0, 0, 7.556) RELATIVE arm1*/

/*============================================================*/

/* Guide N5*/
COMPONENT Guide5 = Guide(
    w1=0.05, 
    h1=0.1, 
    w2=0.05, 
    h2=0.1, 
    l=0.965, 
    m=2.5, 
    R0=Gu_R, 
    Qc=Gu_Qc, 
    W=Gu_W, 
    alpha=Gu_alpha)
AT (0,0,7.572) RELATIVE arm1

/*============================================================*/

/* Curved Guide N6*/
/*The component Bender taken from G. Granroth gives 73% transmittion at 10meV.*/
/* Too optimistic??? */
COMPONENT Guide6 = Bender(
          w=0.05,h=0.10, r=2000.0, l = 14.98, d=0.001,k=1,
          R0a=Gu_R, Qca=Gu_Qc, ma=2.5, alphaa=Gu_alpha, Wa=Gu_W,
          R0i=Gu_R, Qci=Gu_Qc, mi=3.5, alphai=Gu_alpha, Wi=Gu_W,
          R0s=Gu_R, Qcs=Gu_Qc, ms=2.5, alphas=Gu_alpha, Ws=Gu_W)
AT (0,0,8.543) RELATIVE arm1 

/*============================================================*/

/* Guide N7*/
COMPONENT Guide7 = Guide(
    w1=0.05, 
    h1=0.1, 
    w2=0.05, 
    h2=0.1, 
    l=7.0, 
    m=3.0, 
    R0=Gu_R, 
    Qc=Gu_Qc, 
    W=Gu_W, 
    alpha=Gu_alpha)
AT (0,0,23.544) RELATIVE arm1 

/*============================================================*/

/* Guide N8 funnel*/
COMPONENT Guide8 = Guide(
    w1=0.05, 
    h1=0.1, 
    w2=0.035, 
    h2=0.0765, 
    l=2.447, 
    m=3.5, 
    R0=Gu_R, 
    Qc=Gu_Qc, 
    W=Gu_W, 
    alpha=Gu_alpha)
AT (0,0,30.545) RELATIVE arm1 

/*============================================================*/

/*Chopper #3  Distance Moderator - CH#3 = 33020 mm  */
COMPONENT Chopper3=DiskChopper(
	radius=0.25, 
	theta_0=14.0, 
	yheight=0, 
	nu=f3, 
	nslit=1, 
	delay=f3_tof)
AT (0,0,33.020) RELATIVE arm1

/*============================================================*/
/*Fiction Tof monitor for the test purposes only*/
/*TOF step = 1 muS, must be the same everywhere to compare intensity */
/*COMPONENT d3_monitor_tof = TOF_monitor(
    nt=3001, 
    filename="d3_tof_5mm_60Hz.det", 
    xmin=-0.005, 
    xmax=0.005, 
    ymin=-0.005, 
    ymax=0.005, 
    tmin=40500,
    tmax=43500,
    restore_neutron=1)
AT (0, 0, 33.021) RELATIVE arm1*/


/*============================================================*/

/* Guide N9 funnel*/
COMPONENT Guide9 = Guide(
    w1=0.0332, 
    h1=0.076, 
    w2=0.0218, 
    h2=0.0597, 
    l=1.700, 
    m=3.5, 
    R0=Gu_R, 
    Qc=Gu_Qc, 
    W=Gu_W, 
    alpha=Gu_alpha)
AT (0,0,33.025) RELATIVE arm1

/*============================================================*/


/* Chopper #4  DoubleDisk. Distance Moderator - CH#4 = 34784 mm */
/*                                                              */
/*theta_0  	deg  	Angular width of the slits. 	 			*/
/*radius 	m 	Radius of the disc 	 					        */
/*yheight 	m 	Slit height (if = 0, equal to R).              	*/
/*nslit 	1 	Number of slits					       			*/
/* Slits: 9 deg, 4.4 deg, 2 deg 			                	*/ 

COMPONENT Chopper41=DiskChopper(
    radius=0.2825,
    yheight=0.065, 
    theta_0=slits41,
    nslit=1,
    nu=-f41,  
    delay=f41_tof)
AT (0,0,34.784) RELATIVE arm1

COMPONENT Chopper42=DiskChopper(
    radius=0.2825,
    yheight=0.065, 
    theta_0=slits42, 
    nslit=1,
    nu=f42,  
    delay=f42_tof)
AT (0,0,34.785) RELATIVE arm1

/*============================================================*/

/*Beam monitor #3: Tof monitor */
/*TOF step = 1 muS, must be the same everywhere to compare intensity */
/*COMPONENT monitor3_tof = TOF_monitor(
    nt=3001, 
    filename="monitor3_tof.dat", 
    xmin=-0.05, 
    xmax=0.05, 
    ymin=-0.05, 
    ymax=0.05, 
    tmin=42000,
    tmax=45000,
    restore_neutron=1)
AT (0, 0, 34.836) RELATIVE arm1*/

/*COMPONENT monitor3_psd = PSD_monitor(
    nx=100, 
    ny=100, 
    filename="monitor3_psd.det", 
    xmin=-0.05, 
    xmax=0.05, 
    ymin=-0.1, 
    ymax=0.1, 
    restore_neutron=1)
AT (0, 0, 34.787) RELATIVE arm1*/

/*============================================================*/

/* Guide N10 funnel*/
COMPONENT Guide10 = Guide(
    w1=0.0211, 
    h1=0.0587, 
    w2=0.0152, 
    h2=0.0503, 
    l=0.875, 
    m=3.5, 
    R0=Gu_R, 
    Qc=Gu_Qc, 
    W=Gu_W, 
    alpha=Gu_alpha)
AT (0,0,34.853) RELATIVE arm1


/*============================================================*/

/* Guide N11 stright  "old"   */
/*only one guide, either new or old, should be uncommented! */ 

/*COMPONENT Guide11 = Guide(
    w1=0.015, 
    h1=0.05, 
    w2=0.015, 
    h2=0.05, 
    l=0.220, 
    m=4.0, 
    R0=Gu_R, 
    Qc=Gu_Qc, 
    W=Gu_W, 
    alpha=Gu_alpha)
AT (0,0,35.733) RELATIVE arm1*/

/* Guide N11 tapering*/
COMPONENT Guide11 = Guide_tapering(
	l=0.342,
    option="file=guide11-focus.txt",
	R0=Gu_R,
    Qcx=0.0219,
    Qcy=0.0219,
    alphax=6.07,
    alphay=6.07,
    mx=6.0,
    my=6.0,
    W=0.003)
AT (0,0,35.733) RELATIVE arm1



/*============================================================*/
/* Here, a secondary arm - or reference point, placed  */
/* at the sample position.                            */
COMPONENT arm2 = Arm()
At (0,0,36.264) RELATIVE arm1 
/*============================================================*/

/* Vanadium sample, an isotropic scatterer.   */
COMPONENT target = V_sample(
    thickness = 0.004, 
    radius = 0.015,
    yheight = 0.05, 
    pack = 1,  
    focus_aw = 265, 
    focus_ah = 35.0, 
    target_x = 0, target_y = 0, target_z = 3.5)
  AT (0,0,0) RELATIVE arm2


/*============================================================*/

/*Fiction Tof monitor for the test purposes only*/
/*TOF step = 1 muS, must be the same everywhere to compare intensity */
/*COMPONENT sample_position_tof = TOF_monitor(
    nt=401, 
    filename="sample_position_tof.dat", 
    xmin=-0.005, 
    xmax=0.005, 
    ymin=-0.005, 
    ymax=0.005, 
    tmin=45400,
    tmax=45800,
    restore_neutron=1)
AT (0, 0, 0.00) RELATIVE arm2*/


COMPONENT det_position_tof = TOF_monitor(
    nt=401, 
    filename="det_position_tof.dat", 
    xmin=-0.25, 
    xmax=0.25, 
    ymin=-0.5, 
    ymax=0.5, 
    tmin=49800,
    tmax=50200,
    restore_neutron=1)
AT (0, 0, 3.50) RELATIVE arm2

COMPONENT detector_position_psd = PSD_monitor(
    nx=100, 
    ny=100, 
    filename="detector_psd.dat", 
    xmin=-0.25, 
    xmax=0.25, 
    ymin=-0.5, 
    ymax=0.5, 
    restore_neutron=1)
AT (0, 0, 3.50) RELATIVE arm2


/* TOFRes_sample Component   */
/*COMPONENT tofres_sample = TOFRes_sample(
    thickness=0.001,
    radius=0.01,
    yheight=0.05,
    target_x = 0, target_y = 0, target_z = 1.0,
    time_bin = 45628,
    time_width = 1,
    focus_xw = 0.026,
    focus_yh = 0.032)
  AT (0,0,0) RELATIVE arm2
*/

/*The detector size -s about one pixel */
/*COMPONENT res_monitor = Res_monitor(
    res_sample_comp=tofres_sample, 
    filename="Output.res", 
    options="cylinder",
    xwidth=0.0254, 
    yheight=0.0313)
AT (0, 0, 1.0) RELATIVE arm2
*/
/*============================================================*/



COMPONENT tof2e_sample = TOF2E_monitor(
    nE=151, 
    filename="det_position_E", 
    xmin=-0.25, 
    xmax=0.25, 
    ymin=-0.5, 
    ymax=0.5, 
    Emin=3.25, 
    Emax=3.4, 
    T_zero=T0, 
    L_flight=39.762, 
    restore_neutron=1)
AT (0, 0, 3.52) RELATIVE arm2

/*COMPONENT sample_position_psd = PSD_monitor(
    nx=100, 
    ny=100, 
    filename="sample_psd.dat", 
    xmin=-0.025, 
    xmax=0.025, 
    ymin=-0.05, 
    ymax=0.05, 
    restore_neutron=1)
AT (0, 0, 0.02) RELATIVE arm2

COMPONENT divergence_monitor = Divergence_monitor(
    nh=100, 
    nv=100, 
    filename="sample_div.dat", 
    xmin=-0.025, 
    xmax=0.025, 
    ymin=-0.05, 
    ymax=0.05, 
    maxdiv_h=4.0,
    maxdiv_v=4.0,
    restore_neutron=1)
AT (0, 0, 0.03) RELATIVE arm2*/

/*============================================================*/


/* 2D	real CNCS detector				*/
/* consists from 288 tubes from 3.814 deg to 132.606	*/
/* 16 tubes cover 7.18 degrees -> 1 tube : 0.4487	*/
/* length is 2 meters, pixels: 128 vert. resolution: 1.56cm*/
/* distance between sample and detector: 3.5 m		*/

/*COMPONENT CNCS_mon1 = Monitor_nD(
    xwidth = 7.0, 
    yheight = 2.0, 
    zdepth = 0,
    options = "banana, theta limits=[3.814,132.606] bins=288, y bins=128, file = CNCS_det, verbouse, parallel")
AT (0,0,0) RELATIVE arm2*/				


FINALLY
%{
		printf("*********Summary************************\n");
		printf("         TOF[s]  \n");
		printf("CH1     %10.5f \n",f1_tof);
		printf("CH2     %10.5f \n",f2_tof);
		printf("CH3     %10.5f \n",f3_tof);
		printf("CH41    %10.5f \n",f41_tof);
		printf("CH42    %10.5e \n",f42_tof);
        printf("T0      %10.5f \n",T0);
        printf("Tdetector     %10.5f \n",det_tof);
		printf("*****************************************\n");
%}
END



